# Copyright (C) 2024-2025  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

source include/formats.foma

define MapRomanNumeral 
ESRomanMapping -> || .#. _ .#. ; 

define SpanishRoman 
[{II}:segundo|{III}:tercero|{IV}:cuarto|{VI}:sexto|{VII}:séptimo|{VIII}:octavo|{IX}:noveno] @-> || .#. _ .#. ; 

#define Separator %/ ; 
define ProcNumChar [ %-:menos |
%+: más |
%$: dólares |
%.: punto |
%/: barra |
[%/]^2: [barra barra]] ; 

#define Year [Digit^<5] ; 
#define DayOrMonth [Digit^<3] ; 
#define FormatedDate [DayOrMonth Separator DayOrMonth Separator Year] ; 
#define Number FormatedDate | [UnformattedNumber (OrdChar)] | ESRomanNumeral ; 

define MaleOrdUnit 
%0:cerésimo | 
1:primero | 
2:segundo | 
3:tercero | 
4:cuarto | 
5:quinto | 
6:sexto | 
7:séptimo | 
8:octavo | 
9:noveno ; 

define FemaleOrdUnit 
%0:cerésima | 
1:primera | 
2:segunda | 
3:tercera | 
4:cuarta | 
5:quinta | 
6:sexta | 
7:séptima | 
8:octava | 
9:novena ; 


define Teen 
1:once | 
2:doce | 
3:trece | 
4:catorce | 
5:quince | 
6:dieciseis | 
7:diecisiete | 
8:dieciocho | 
9:diecinueve ; 

define Twenti 
1:veintiuno | 
2:veintidós | 
3:veintitrés | 
4:veinticuatro | 
5:veinticinco | 
6:veintiseis | 
7:veintisiete | 
8:veintiocho | 
9:veintinueve ; 

define Ten 
1:diez | 
2:veinte | 
3:treinta | 
4:cuarenta | 
5:cincuenta | 
6:sesenta | 
7:setenta | 
8:ochenta | 
9:noventa ; 

define MaleOrdTen 
1:décimo | 
2:vigésimo | 
3:trigésimo | 
4:cuadragésimo | 
5:quincuagésimo | 
6:sexagésimo | 
7:septagésimo | 
8:octogésimo | 
9:nonagésimo ; 

define FemaleOrdTen 
1:décima | 
2:vigésima | 
3:trigésima | 
4:cuadragésima | 
5:quincuagésima | 
6:sexagésima | 
7:septagésima | 
8:octogésima | 
9:nonagésima ; 


define Hundred 
1:ciento | 
2:doscientos | 
3:trescientos | 
4:cuatrocientos | 
5:quinientos | 
6:seiscientos | 
7:setecientos | 
8:ochocientos | 
9:novecientos ; 

define MaleOrdHundred 
1:centésimo | 
2:docentésimo | 
3:trecentésimo | 
4:cuatringentésimo | 
5:quingentésimo | 
6:sessentésimo | 
7:setingentésimo | 
8:octocentésimo | 
9:noningentésimo ; 

define FemaleOrdHundred 
1:centésima | 
2:docentésima | 
3:trecentésima | 
4:quatringentésima | 
5:quingentésima | 
6:sessentésima | 
7:setingentésima | 
8:octocentésima | 
9:noningentésima ; 

define preTranslate 
[1 mlns] -> [1 mln] || _ ,, 
[1 blns] -> [1 bln] || _ ,, 
[1 tlns] -> [1 tln] || _ ,, 
[1 qlns] -> [1 qln] || _ ; 

define PowerOfThousandSymbol ths|mlns|blns|tlns|qlns|mln|bln|tln|qln ; 
define TenOrHundredSymbol ten|hnd0|hnd ; 
define PowerOfTenSymbol TenOrHundredSymbol|PowerOfThousandSymbol ; 

define OrdPowerOfThousandSymbol thth|mlnth|blnth|tlnth|qlnth ; 
define OrdTenOrHundredSymbol tenth|hndth ; 
define OrdPowerOfTenSymbol OrdTenOrHundredSymbol|OrdPowerOfThousandSymbol ; 

define SeparatePowerOfThousand(s) [..] -> s || Digit _ Digit^3 [\dec & \Digit] ; 

define RemoveSilentGroups 
%0^3 (PowerOfThousandSymbol) @-> 0 || _ ?* wnum ; 

define SeparateHundreds 
[..] -> hnd || Digit _ Digit^2 ?* wnum ; 

define Mark100s 
hnd -> hnd0 || 1 _ {00} ; 

define SeparateTens 
[..] -> ten || Digit _ Digit ?* wnum ; 

define MarkTenth 
ten -> tenth || 
Digit _ Digit ths Digit hnd Digit ten Digit wnum ord OrdChar , 
Digit _ Digit wnum ord OrdChar ; 

define MarkHundredth 
[hnd|hnd0] -> hndth || Digit _ Digit tenth ; 

define MarkThousandth 
ths -> thth || Digit _ [Digit hndth] | OrdChar ; 

define MarkOrdPowerOfThousand 
mlns:mlnth | blns:blnth | tlns:tlnth | qlns:qlnth -> || _ ?* wnum ord OrdChar ; 

define RemoveZeros 
%0 ([TenOrHundredSymbol | OrdTenOrHundredSymbol]) @-> 0 || \%. _ ?* wnum ; 

define MarkTeens 
[[1 ten]:0] NZDigit [0:[teen %0]] -> ; 

define MarkTwenties 
[[2 ten]:0] NZDigit [0:[twenti %0]] -> ; 

define InsertAnds 
[..] -> and || 
NZDigit ten _ NZDigit ; 

define RemoveOnes 
1 -> 0 || .#. _ ths | OrdPowerOfThousandSymbol ; 

define PowerOfThousandSymbolsToSingular 
[mlns:mln|blns:bln|tlns:tln|qlns:qln] -> || [[PowerOfThousandSymbol (and)]|wnum] 1 _ ; 

define tagNumberTypes 
  [FormattedNumber|UnformattedNumber] @-> ... wnum || .#. _ (%, Digit+)  .#. ,, 
	UnformattedNumber @-> ... wnum || .#. _ (%. Digit+)  .#.  ,, 
	[FormattedNumber|UnformattedNumber] @-> ... wnum ord || .#. _ OrdChar .#. ; 

define tagCascade 
   [  [%,|%.] -> ... dp || wnum  _ Digit+ .#. ] 
  .o. [ [%.]:0  -> || _ ?* wnum ] 
	.o. [ Digit -> ... dec  || dp Digit* _ .#. ] 
	.o. [ dec -> wnum || dp NZDigit Digit^<3 _ .#. ] 
 	.o. [ Digit-> ... wnum || [.#. | \Digit] Digit* _ [ .#. | %. | %, ]]; 

define tagRules  tagNumberTypes .o. tagCascade; 

define Ords 
#OrdChar -> 0 ,, 
MaleOrdUnit -> || _ wnum ord MaleOrdChar .#. ,, 
FemaleOrdUnit -> || _ wnum ord FemaleOrdChar .#. ,, 
[MaleOrdTen tenth:0] -> || _ ?* wnum ord MaleOrdChar .#. ,, 
[FemaleOrdTen tenth:0] -> || _ ?* wnum ord FemaleOrdChar .#. ,, 
[MaleOrdHundred hndth:0] -> || _ ?* wnum ord MaleOrdChar .#. ,, 
[FemaleOrdHundred hndth:0] -> || _ ?* wnum ord FemaleOrdChar .#. ; 

define Translate [
SpellDigit -> || _ wnum | dec | PowerOfThousandSymbol | OrdPowerOfThousandSymbol ,, 
SpellDigit -> || dp Digit* _ Digit* dec ,, 
Teen [teen:0] -> || _ ,, 
Twenti [twenti:0] -> || _ ,, 
Ten [ten:0] -> || _ ,, 
Hundred [hnd:0] -> || _ ,, 
1 hnd0 -> cien || _ ,, 
and -> y || _ ,, 
%, -> coma || _ ?+ .#. ,, 
%. -> punto || _ ?+ .#. ,, 
[thth:milésimo | mlnth:millonésimo | blnth:billonésimo | tlnth:trillonésimo | qlnth:cuatrillonésimo] @-> || _ ?* wnum ord MaleOrdChar ,, 
[thth:milésima | mlnth:millonésima | blnth:billonésima | tlnth:trillonésima | qlnth:cuatrillonésima] @-> || _ ?* wnum ord FemaleOrdChar 
] .o. [
ths -> mil || _ ,, 
[mln:millón | bln:billón | tln:trillón | qln:cuatrillón] -> || _ ,, 
[mlns:millones | blns:billones | tlns:trillones | qlns:cuatrillones] -> || _
] ; 

define fixes2 
[uno:ún | veintiuno:veintiún] -> || _ [mil|millón|billón|trillón|cuatrillón] ; 

define NumberRules 
SeparatePowerOfThousand(ths) .o. 
SeparatePowerOfThousand(mlns) .o. 
SeparatePowerOfThousand(ths) .o.
SeparatePowerOfThousand(blns) .o. 
SeparatePowerOfThousand(ths) .o.
SeparatePowerOfThousand(tlns) .o. 
SeparatePowerOfThousand(ths) .o.
SeparatePowerOfThousand(qlns) .o.
RemoveSilentGroups .o. 
SeparateHundreds .o. 
Mark100s .o. 
SeparateTens .o. 
MarkTenth .o. 
MarkHundredth .o. 
MarkThousandth .o. 
MarkOrdPowerOfThousand .o. 
MarkTeens .o. 
MarkTwenties .o. 
InsertAnds .o. 
RemoveZeros .o. 
PowerOfThousandSymbolsToSingular .o. 
preTranslate .o. 
RemoveOnes .o. 
Ords .o. 
[%.|%,|wnum] -> cero ... || .#. _ .o. 
Translate .o. 
[[ord OrdChar]:0 -> || wnum _ .#.] .o. 
[[dp:0|dec:0|wnum:0] -> || _] .o. 
fixes2 ; 

regex 
[ProcNumChar | Number] .o. 
SpanishRoman .o. 
MapRomanNumeral .o. 
tagRules .o.
NumberRules ; 