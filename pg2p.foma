# Copyright (C) 2024-2025  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

# Spanish Post g2p with documentation. Important things to consider:
# Phrase Boundary.
define PB %- | .#. ; 
# Word boundary (words of the utterance)
define WB %# | PB ; 
# Vowels for approximants or allophones.
define vocal [a|a0|a1|e|e0|e1|i|i0|i1|o|o0|o1|u|u0|u1|j|w] ; 
# We'll need to remove/fix 00 phonemes, that is done on purpose to preserve comon Spanish unstressed words.
define distinguishStress [
a00: a0 |
e00: e0 |
i00: i0 |
o00: o0 |
u00: u0 |
J: j |
S: s |
X: x ]
@-> || _ ; 

# Here, we apply approximants through the whole utterance, same that we did in g2p but with word boundaries between vowels.
define RemainingApproximants 
b @-> bh || vocal WB _ vocal WB ,, 
d @-> dh || vocal WB _ vocal WB ,, 
g @-> gh || vocal WB _ vocal WB ,, 
v @-> vh || vocal WB _ vocal WB ; 

# Here, to get a better prosody of the voice, we unstress the "enta y" prefix. For example, a speaker doesn't say trÃ©inta y cinco, but treinta y cinco, withouth stressing in the e. So, we will fix this behavior in RHVoice voices.
define StressedNumber 
[t r e1 i0 n t a0]:[t r e0 i0 n t a0]|
[k w a0 r e1 n t a0]:[k w a0 r e0 n t a0]|
[c i0 n k w e1 n t a0]:[c i0 n k w e0 n t a0]|
[s e0 s e1 n t a0]:[s e0 s e0 n t a0]|
[s e0 t e1 n t a0]:[s e0 t e0 n t a0]|
[o0 ts e1 n t a0]:[o0 ts e0 n t a0]|
[n o0 vh e1 n t a0]:[n o0 vh e0 n t a0] ; 
define unstressNumber 
# When treinta, cuarenta, cincuenta, etc... is in the word, accompanied by i (y), remove the stress. For example, cuarenta y...
StressedNumber @-> || WB _ WB i0 ; 
define MaleObjectPronoun 
[[u0 n] | [e0 l] | [e0 s e0] | [e0 s t e0] | [a0 k e1 l]] ; 

define PersonalObjectPronoun 
[m i0] | [t u0] | [s u0] ; 
define object 
[b a0 x o0]: [b a1 x o0] | 
[s o0 b r e0]: [s o1 b r e0] ; 
# Un sobre, el sobre, ese sobre, este sobre, aquel sobre (a paper object)
#Un bajo, el bajo, ese bajo, este bajo, aquel bajo (a music instrument)
# We'll need to stress "bajo" and "sobre" in these five cases, because regarding "bajo" we are talking of the "bass" music instrument instead of the attribution (under), and regarding "sobre" we are talking of the paper object, not the "about" preposition (talk about her / hablar sobre ella, for example).

define HomonymousWords 
object -> || [MaleObjectPronoun|PersonalObjectPronoun] WB _ WB ; 

regex 
distinguishStress .o. 
RemainingApproximants .o. 
unstressNumber .o. 
HomonymousWords ; 