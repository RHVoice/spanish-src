# Copyright (C) 2024  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define TranscribeVowel 
a:a | 
á:a1 | 
Á:a1 | 
e:e | 
é:e1 | 
É:e1 | 
i:i | 
í:i1 | 
Í:i1 | 
o:o | 
ó:o1 | 
Ó:o1 | 
u:u | 
ú:u1 | 
Ú:u1 ; 

define TranscribeConsonant 
b:b | 
c:c | 
{ca}:{ka} | # casa, finca.
[c a1]:[k a1] | # acá.
{co}:{ko} | # coche, cinco
[c o1]:[k o1] | # marcó, achicó.
[c u]:{ku} | # curso, cuveta
[c u1]:[k u1] | # cúspide.
{ch}:ts | # Chico, cholo
d:d | 
f:f | 
g:g | # siglo
[g u e]:[g e] | # sigue, guerra
[g u e1]:[g e1] | # portugués
[g e]:[x e] | # Gente, generación
[g e1]:[x e1] | # génesis
[g u i]:[g i] | # guiso, guineo
[g u i1]:[g i1] | # guía
[g i]:[x i] | 
[g i1]:[x i1] | 
[g u a]:[g w a] | # antigua, guarida
[g ü e]:[g w e] | # antigüedad, desagüe
[g ü e1]:[g w e1] | # averigüé
[g ü i]:[g w i] | # agüita
[g ü i1]:[g w i1] | # Lingüística.
[g u o]:[g w o] | # antiguo
h: "" | # H is a sylent consonant, so no phoneme.
j:x | 
k:k | 
l:l | 
{ll}:ll | # aquella
m:m | 
n:n | 
ñ:ni | # año, otoño
p:p | 
{ph}:f | 
r:r | 
{rr}:rr | # Rato, tierra
s:s | 
{sh}:sh | # especialmente para palabras estrangeras (foreigth words)
t:t | 
ŭ:u | 
v:v | 
z:zh | # previously c, but z is a bit long sound diferent than c (ce, ci).
q:k | 
[q u e]:[k e] | # queso, buque
[q u e1]:[k e1] | # qué
[q u i]:[k i] | # quito, quijada
[q u i1]:[k i1] | # sequía
w:w | 
x:{ks} | # Existe, éxito
y:y ; 

define SpanishRules 
# closed vowel i
{ia}:[j a] | # biacrusis, referencia, viciándose, diafragma, fiar, sequia, cecilia, ...
[i a1]:[j a1] | # viciándose, viciado, desquiciado
{ie}:[j e] | # bien, ciego, diente, fiera, quieto, lienzo, pie
[i e1]:[j e1] | # arrepiéntanse, vicié
{io}:[j o] | # biología, servicio, fiona, kiosco, julio, leccionario
[i o1]:[j o1] | # siguió, biósfera, asociación, institución, adiós, selección, actualización
{iu}:[j u] | # viudo, quiubo, veintiuno
[i u1]:[j u1] | # veintiún
# closed vowel u
{ua}:[w a] | # estatua
[u a1]:[w a1] | # guácala
{ue}:[w e] | # encuesta
[u e1]:[w e1] | # muérgano
{ui}:[w i] | # juicio
[u i1]:[w i1] | # cuídate
{uo}:[w o] | # reciduo
[u o1]:[w o1] | # continuó
# c to k exceptions:
[c c]:[k c] | # selección, leccionario
[c d]:[k d] | # Anécdota.
[c k]:k | # talkback.
[c l]:[k l] | # claro (klaro, not claro)
[c m]:[k m] | # francmasonería.
[c n]:[k n] | # cneorácea, tecnología.
[c p]:[k p] | # yucpa
[c r]:[k r] | # secreto (sekreto, not secreto)
[c s]:[k s] | # clics.
[c t]:[k t] | # actualización (aktwalizhazhjo1n, not actwalicacjo1n)
{cy}:[c i] | # Cynthia.
[t ts]:ts | # switch
[d o w]:[d o u] ; # Change ow sound. Window, windows.

define WordStartRules 
{any} -> {ani} || .#. _ ,, # Especially for English text.
{psi} -> {si} || .#. _ ,, # psicología, psiquiatra.
[ts r i] -> [k r i] || .#. _ ,, # Christian, christina.
[d e sh] -> [d e s] || .#. _ ,, # des prefix. Deshabilitar, deshacer.
r -> [rr] || .#. _, [l|n|s] _ ; # at the start: rato, rico, roma, all words starting r needs to be a RR sound. However, in words that contains l, n or s anywere, needs to be a rr sound, too.

define WordMidRules 
# when y is a standalone word, converts to i because it is an open vowel.
y -> i || .#. _ .#. ; 

define WordEndRules 
# handling of closed vowel y to the end of word. Pley, soy, estoy, voy, hay, etc.
{ay} -> [a yc] || _ .#. ,, # hay alguien aquí?
{ey} -> [e yc] || _ .#. ,, # vacabuey, rey, ley.
{oy} -> [o yc] || _ .#. ,, # voy, soy, estoy.
{uy} -> [u j] || _ .#. ,, # muy, cuy, Chuy, juyayay.
# The following is especially for foreign words.
{by} -> [b i] || _ .#. ,, 
{dy} -> [d i] || _ .#. ,, 
{ky} -> [k i] || _ .#. ,, 
{ly} -> [l i] || _ .#. ,, 
{my} -> [m i] || _ .#. ,, 
{ny} -> [n i] || _ .#. ,, # Danny, Ronny.
{py} -> [p i] || _ .#. ,, 
{ry} -> [r i] || _ .#. ,, # cherry.
{sy} -> [s i] || _ .#. ,, 
{ty} -> [t i] || _ .#. ,, 
# now, when a words ends with c, convert to k.
c -> k || _ .#. ; # cómic, tictac.

define exceptions 
# a way to preserve most comon homonymous words for later conversion in pg2p.
[{hacia} : [a c i a00]] | 
[{hasta} : [a s t a00]] | 
[{como} : [k o m o00]] | 
[{cuando} : [k w a n d o00]] | 
[{cuanto} : [k w a n t o00]] | 
[{desde} : [d e s d e00]] | 
[{donde} : [d o n d e00]] | 
[{porque} : [p o r k e00]] | 
[{quienes} : [k i e n e00 s]] | 
# Other exceptions:
[[{wi} (%-) {fi}] : [w a i f a1 yc]] | 
[{a.c.} : [a n t e s d e k r i1 s t o]] | 
[{a.m.} : [d e l a m a ni a1 n a]] | 
[{d.c.} : [d e s p w e1 s d e k r i1 s t o]] | 
[{p.m.} : [d e l a t a1 r d e]] | 
[{etc.} : [e t c e1 t e r a]] @-> || .#. _ ; 

define Stage1 
TranscribeVowel @-> ; 

define Stage2 
TranscribeConsonant @-> ; 

define Stage3 
SpanishRules @-> ; 

source special_chars.foma

define rules 
NormalizeCharactersG2p .o.
Stage1 .o.
Stage2 .o.
Stage3 .o.
WordStartRules .o.
WordMidRules .o.
WordEndRules ;

source stress.foma

regex 
exceptions .o. 
rules .o. 
unstress .o. 
SpanishStress .o. 
SpanishStress2 .o. 
UnstressWords ; 