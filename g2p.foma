# Copyright (C) 2024  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

define NormalizeCharacters 
# These are some strange characters that i found, for example, in social media, these are alphabet letters but a tonal/visual representation, like genders. So, although these symbols are set up in spell, we need to pass to g2p so that it treats them as normal letters and forms words with these.
[ą|ă|Ą|Ă|ā|Ā]:a | 
[Ć|ć|Ĉ|ĉ|Ċ|ċ|Č|č]:c | 
[Ď|ď|Đ|đ]:d | 
[Ē|ē|Ĕ|ĕ|Ė|ė|Ę|ę|Ě|ě]:e | 
[Ĝ|ĝ|Ğ|ğ|Ġ|ġ|Ģ|ģ]:g | 
[Ĥ|ĥ|Ħ|ħ]:h | 
[Ĩ|ĩ|Ī|ī|Ĭ|ĭ|Į|į|İ|ı]:i | 
[Ĵ|ĵ]:j | 
[Ķ|ķ]:k | 
[Ĺ|ĺ|Ļ|ļ|Ľ|ľ|Ł|ł]:l | 
[Ń|ń|Ņ|ņ|Ň|ň]:n | 
[Ō|ō|Ŏ|ŏ|Ő|ő|Œ|œ]:o | 
[Ŕ|ŕ|Ŗ|ŗ|Ř|ř]:r | 
[Ś|ś|Ŝ|ŝ|Ş|ş]:s | 
[Ţ|ţ|Ť|ť|Ŧ|ŧ]:t | 
[Ũ|ũ|Ū|ū|Ŭ|ŭ|Ů|ů|Ű|ű|Ų|ų]:u | 
[Ŵ|ŵ]:w | 
[Ŷ|ŷ]:y | 
[Ź|ź|Ż|ż]:z ; 

define TranscribeVowel 
a:a | 
á:a1 | 
Á:a1 | 
e:e | 
é:e1 | 
É:e1 | 
i:i | 
í:i1 | 
Í:i1 | 
o:o | 
ó:o1 | 
Ó:o1 | 
u:u | 
ú:u1 | 
Ú:u1 ; 

define TranscribeConsonant 
b:b | 
c:c | 
{ca}:{ka} | # casa, finca.
[c a1]:[k a1] | # acá.
{co}:{ko} | # coche, cinco
[c o1]:[k o1] | # marcó, achicó.
[c u]:{ku} | # curso, cuveta
[c u1]:[k u1] | # cúspide.
{ch}:ts | # Chico, cholo
d:d | 
f:f | 
g:g | # siglo
[g u e]:[g e] | # sigue, guerra
[g u e1]:[g e1] | # portugués
[g e]:[x e] | # Gente, generación
[g e1]:[x e1] | # génesis
[g u i]:[g i] | # guiso, guineo
[g u i1]:[g i1] | # guía
[g i]:[x i] | 
[g i1]:[x i1] | 
[g u a]:[g w a] | # antigua, guarida
[g ü e]:[g w e] | # antigüedad, desagüe
[g ü i]:[g w i] | # agüita
[g u o]:[g w o] | # antiguo
h: "" | # H is a sylent consonant, so no phoneme.
j:x | 
k:k | 
l:l | 
{ll}:ll | # aquella
m:m | 
n:n | 
ñ:ni | # año, otoño
p:p | 
r:r | 
{rr}:rr | # Rato, tierra
s:s | 
{sh}:sh | # especialmente para palabras estrangeras (foreigth words)
t:t | 
ŭ:u | 
v:v | 
z:zh | # previously c, but z is a bit long sound diferent than c (ce, ci).
q:k | #Todo: special handling for q. When after q has "u" as next letter, the "u" is a silent vowel. So, u needs to be removed and replaced with w, closed vowel of u.
[q u e]:[k e] | # queso, buque
[q u e1]:[k e1] | # qué
[q u i]:[k i] | # quito, quijada
[q u i1]:[k i1] | # sequía
w:w | 
x:{ks} | # Existe, éxito
y:y ; 

define SpanishRules 
# au, eu, ou.
{au}:au | # audacia, Aurelia, bau.
{eu}:eu | # neuronal, reusar, deuda.
# There aren't many words with ou in Spanish, so commenting for now to avoid less observation in any corpus.
#{ou}:ou | # estadounidense, glamour.
# ai, ei, oi.
{ai}:ai | # paisage, aire, Haití.
{ei}:ei | # aceite, reintentar, reiniciar.
{oi}:oi | # coincidir, espermatozoide, androide.
# closed vowel i
{ia}:[j a] | # biacrusis, referencia, viciándose, diafragma, fiar, sequia, cecilia, ...
[i a1]:[j a1] | # viciándose, viciado, desquiciado
{ie}:[j e] | # bien, ciego, diente, fiera, quieto, lienzo, pie
[i e1]:[j e1] | # arrepiéntanse, vicié
{io}:[j o] | # biología, servicio, fiona, kiosco, julio, leccionario
[i o1]:[j o1] | # siguió, biósfera, asociación, institución, adiós, selección, actualización
{iu}:[j u] | # viudo, quiubo, veintiuno
[i u1]:[j u1] | # veintiún
# closed vowel u
{ua}:[w a] | # estatua
[u a1]:[w a1] | # guácala
{ue}:[w e] | # encuesta
[u e1]:[w e1] | # muérgano
{ui}:[w i] | # juicio
[u i1]:[w i1] | # cuídate
{uo}:[w o] | # reciduo
[u o1]:[w o1] | # continuó
# c to k exceptions:
[c c]:[k c] | # selección, leccionario
[c l]:[k l] | # claro (klaro, not claro)
[c r]:[k r] | # secreto (sekreto, not secreto)
[c t]:[k t] ; # actualización (aktwalizhazhjo1n, not actwalicacjo1n)

define WordStartRules 
{psi} -> {si} || .#. _ ,, # psicología, psiquiatra.
r -> [rr] || .#. _ ; # rato, rico, roma, all words starting r are a RR sound.

define WordMidRules 
# when y is a standalone word, converts to i because it is an open vowel.
y -> i || .#. _ .#. ; 

define WordEndRules 
# handling of closed vowel y to the end of word. Pley, soy, estoy, voy, hay, etc.
{ay} -> ai || _ .#. ,, # hay alguien aquí?
{ey} -> ei || _ .#. ,, # vacabuey, rey, ley.
{oy} -> oi || _ .#. ,, # voy, soy, estoy.
{uy} -> [u j] || _ .#. ,, # muy, cuy, Chuy, juyayay.
# The following is especially for foreign words.
{ly} -> [l i] || _ .#. ,, 
{py} -> [p i] || _ .#. ,, 
{ty} -> [t i] || _ .#. ,, 
# now, when a words ends with c, convert to k.
c -> k || _ .#. ; # cómic, tictac.

define Stage1 
NormalizeCharacters @-> ,,
TranscribeVowel @-> ; 

define Stage2 TranscribeConsonant @-> ; 
define Stage3 SpanishRules @-> ; 

source stress.foma

regex 
Stage1 .o.
Stage2 .o.
Stage3 .o.
WordStartRules .o.
WordMidRules .o.
WordEndRules .o.
SpanishStress .o.
#SpanishStress2.o.
unstress ; 