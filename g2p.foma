# Copyright (C) 2024-2025  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

# Spanish Grapheme to Phoneme (g2p) for RHVoice.

source include/phon/stress.foma

define UnstressedWords 
# This is the special word list to be unstressed. That means, these words doesn't have intonation / stress mark, marked by 1 (ones).
{aunque} | {hacia} | {hasta} | {bajo} | {contra} | {como} | {cuando} | {cuanto} | {desde} | {donde} | {durante} | 
[{haya}(n|s)] | 
{hemos} | {mediante} | {mientras} | {pero} | {porque} | 
[{quien}({es})] | 
{sino} | {sobre} | 
[{una}(s)] | {unos} | {ante} | 
[{cuya}(s)] | [{cuyo}(s)] | {entre} | 
[{esa}(s)] | [{esta}(s)] | {ese} | {esos} | {este} | {estos} | 
[{nuestro}(s)] | [{nuestra}(s)] | [{vuestro}(s)] | [{vuestra}(s)] | 
{para} | 
{al} | {han} | {has} | {con} | {de} | {del} | {en} | {el} | {les} | {le} | {lo} | {los} | {la} | {las} | {me} | {nos} | {os} | {por} | {pues} | {mi} | {mis} | {ni} | {que} | {se} | {si} | {sin} | {su} | {sus} | {te} | {ti} | {tu} | {tus} | {un} | {mas} | {tras} ; 

define UnstressedWordsTag 
[..] @-> Uns || .#. UnstressedWords _ .#. ,, # Prevent touching these words by the stress rule, just phonemize only.
{hace} @-> MakeTag || .#. _ .#. ; # this is a special case we will need to handle latter for time reference to predict stress. "Hace unas horas", "hace mucho frío".

define StressedForms 
# A vowel preceded by h should be stressed, and a h preceded by vowel as well, except for "ha" and "he" which are conjuntions.
[..] @-> Stressf || .#. [a|e|i|o|u] h _ .#., .#. h [i|o|u] _ .#. , .#. i _ .#. ; 

define deshChange 
# Let's remove the h when it comes to Spanish prefixes des- and tras-, for example: deshacer, trashumar.
# Because this is pronounced as s (de /s acer), not sh (de /sh acer). We have a phoneme called sh for foreign words.
# In summary, prevent sh in the following prefixes:
h:0 @-> || .#. [{des}|{tras}] _ vocal ; 

# general transcription rules:

define TranscribeVowel 
[a|ä|â|à|ã]:a | 
á:a1 | 
[e|ë|è|ê]:e | 
é:e1 | 
[i|î|ï|ì]:i | 
í:i1 | 
[o|ö|ò|ô|õ]:o | 
ó:o1 | 
[u|ù|û]:u | 
ú:u1 ; 

define TranscribeConsonant 
b:b | 
c:c | 
ç:s | 
{ca}:{ka} | # casa, finca.
[c a1]:[k a1] | # acá.
{co}:{ko} | # coche, cinco
[c o1]:[k o1] | # marcó, achicó.
[c u]:{ku} | # curso, cuveta
[c u1]:[k u1] | # cúspide.
{ch}:ch | # Chico, cholo
d:d | 
f:f | 
g:g | # siglo
[g u e]:[g e] | # sigue, guerra
[g u e1]:[g e1] | # portugués
[g e]:[x e] | # Gente, generación
[g e1]:[x e1] | # génesis
[g u i]:[g i] | # guiso, guineo
[g u i1]:[g i1] | # guía
[g i]:[x i] | 
[g i1]:[x i1] | 
[g u a]:[g w a] | # antigua, guarida
[g u a1]:[g w a1] | # aguábamos
[g ü e]:[g w e] | # antigüedad, desagüe
[g ü e1]:[g w e1] | # averigüé
[g ü i]:[g w i] | # agüita
[g ü i1]:[g w i1] | # Lingüística.
[g u o]:[g w o] | # antiguo
h: modifier | # the letter h, apart from being a silent consonant, fulfills a function, especially when it is in the middle of vowels, diphthongs. We will use the modifier tag for it. For the rest of cases, h is silent and removed, h is nothing, phonemically.
j:x | 
k:k | 
l:l | 
{ll}:ll | # aquella
m:m | 
n:n | 
ñ:ni | # año, otoño
p:p | 
{ph}:f | 
r:r | 
{rr}:rr | # Rato, tierra
s:s | 
{sh}:sh | # especialmente para palabras estrangeras (foreign words)
t:t | 
ŭ:u | 
ü:u | # In cases outside g rules.
v:v | 
z:zh | # previously c, but z is a bit long sound diferent than c (ce, ci).
q:k | 
[q u e]:[k e] | # queso, buque
[q u e1]:[k e1] | # qué
[q u i]:[k i] | # quito, quijada
[q u i1]:[k i1] | # sequía
w:u | # Change to a u sound for some cases.
x:{ks} | # Existe, éxito
y:YAsVowel | # Y can be a vowel or a consonant. When "y" is followed by a vowel, it is a phoneme, /y. When not, it is a vowel, /i.
{ya}:[Y a] | 
[y a1]:[Y a1] | 
{ye}:[Y e] | 
[y e1]:[Y e1] | 
{yi}:[Y i] | 
[y i1]:[Y i1] | 
{yo}:[Y o] | 
[y o1]:[Y o1] | 
{yu}:[Y u] | 
[y u1]:[Y u1] ; 

define SpanishRules 
# ai, ei, oi.
{ai}:[a j] | # aire, baila.
[a1 i]:[a1 j] | # abastezcáis
{ei}:[e j] | # leiri.
[e1 i]:[e1 j] | # aféitense.
{oi}:[o j] | # paranoico, oiga.
# au, eu.
{au}:[a w] | # audio, laura.
{eu}:[e w] | # deuda, neurona.
# We doesn't add ou because there are not comon words with these.
# closed vowel i
{ia}:[j a] | # biacrusis, referencia, viciándose, diafragma, fiar, sequia, cecilia, ...
[i a1]:[j a1] | # viciándose, viciado, desquiciado
{ie}:[j e] | # bien, ciego, diente, fiera, quieto, lienzo, pie
[i e1]:[j e1] | # arrepiéntanse, vicié
{io}:[j o] | # biología, servicio, fiona, kiosco, julio, leccionario
[i o1]:[j o1] | # siguió, biósfera, asociación, institución, adiós, selección, actualización
{iu}:[j u] | # viudo, quiubo, veintiuno
[i u1]:[j u1] | # veintiún
# closed vowel u
{ua}:[w a] | # estatua
[u a1]:[w a1] | # guácala
{ue}:[w e] | # encuesta
[u e1]:[w e1] | # muérgano
{ui}:[w i] | # juicio
[u i1]:[w i1] | # cuídate
{uo}:[w o] | # reciduo
[u o1]:[w o1] | # continuó
# Brian, not bryan:
[b r y]:[b r j] | 
# c to k exceptions:
[c c]:[k c] | # selección, leccionario
[c d]:[k d] | # Anécdota.
[c x]:[k x] | # discjockey.
[c k]:k | # talkback.
[c l]:[k l] | # claro (klaro, not claro)
[c m]:[k m] | # francmasonería.
[c n]:[k n] | # cneorácea, tecnología.
[c p]:[k p] | # yucpa
[c r]:[k r] | # secreto (sekreto, not secreto)
[c s]:[k s] | # clics.
[c t]:[k t] | # actualización (aktwalizhazhjo1n, not actwalicacjo1n)
[c YAsVowel]:[c i] | # Cynthia.
[t ch]:ch | # switch
[a p p]:[a p] ; # For English words.

define WordStartRules 
# source rules from cldr/common/transforms/es-es_FONIPA.xml
{ct} -> t || .#. _ ,, # ctónico
{cz} -> z || .#. _ ,, # czar
{gn} -> n || .#. _ ,, # gnomo
{mn} -> n || .#. _ ,, # mnemotécnico
{pt} -> t || .#. _ ,, # pterodáctilo
{ks} -> s || .#. _ ,, # xilófono
# Matthew's rules:
{pse} -> {se} || .#. _ ,, # seudo.
{psi} -> {si} || .#. _ ,, # psicología, psiquiatra.
[ch r i] -> [k r i] || .#. _ ,, # Christian, christina.
r -> [rr] || .#. _, [l|n|s] _ ; # at the start: rato, rico, roma, all words starting r needs to be a RR sound. However, in words that contains l, n or s anywere, needs to be a rr sound, too. For example: alrededor, enredo, Desramar.

define WordMidRules 
# when y is a standalone word, converts to i because it is an open vowel.
[y|YAsVowel] -> TagYgriega || .#. _ .#. ; 

define WordEndRules 
# handling of closed vowel y to the end of word. Pley, soy, estoy, voy, hay, etc.
[a YAsVowel] -> [a j] || _ (Consonant|modifier) .#. ,, # hay alguien aquí?
[e YAsVowel] -> [e j] || _ (Consonant|modifier) .#. ,, # vacabuey, rey, ley.
[o YAsVowel] -> [o j] || _ (Consonant|modifier) .#. ,, # voy, soy, estoy.
[u YAsVowel] -> [u j] || _ (Consonant|modifier) .#. ,, # muy, cuy, Chuy, juyayay.
# The following is especially for foreign words.
[i n g] -> [i n] || _ .#. ,, 
# now, when a words ends with c, convert to k.
c -> k || _ .#. ; # cómic, tictac.

define approximants 
# In Spanish, there are about 4 approximants, that are voiced consonants that occurs in different contexts. The good news, all approximants occurs between vowels.
{b} (modifier) -> bh || vocal _ vocal ,, 
{d} (modifier) -> dh || vocal _ vocal ,, 
{g} (modifier) -> gh || vocal _ vocal ,, 
{v} (modifier) -> bh || vocal _ vocal ; 

define exceptions 
# There are special words having own phonemes.
[[{wi} (%-) {fi}] : [w a0 j f a1 j]] | 
[{a.c.} : [a1 n t e0 s d e0 k r i1 s t o0]] | 
[{a.m.} : [d e0 l a0 m a0 ni a1 n a0]] | 
[{d.c.} : [d e0 s p w e1 s d e0 k r i1 s t o0]] | 
[{p.m.} : [d e0 l a0 t a1 r d e0]] | 
# Weeks, months:
[{lun.} : [l u1 n e0 s]] | 
[{mar.} : [m a1 r t e0 s]] | 
[{mié.} : [m j e1 r k o0 l e0 s]] | 
[{jue.} : [x w e1 v e0 s]] | 
[{vie.} : [v j e1 r n e0 s]] | 
[{sáb.} : [s a1 b a0 d o0]] | 
[{dom.} : [d o0 m i1 n g o0]] | 
[{ene.} : [e0 n e1 r o0]] | 
[{feb.} : [f e0 b r e1 r o0]] | 
# We doesn't add mar. (marzo) because mar (withouth the capital M) is the beach. If someone reads a text and the sentence ends with mar., for example, ayer fui al mar., the language package could interpret: ayer fui al marzo. So, we are avoiding these misunderstandings.
[{abr.} : [a0 b r i1 l]] | 
[{may.} : [m a1 y o0]] | 
[{jun.} : [x u1 n j o0]] | 
[{jul.} : [x u1 l j o0]] | 
[{ago.} : [a g o1 s t o0]] | 
[{sep.} : [s e0 p t j e1 m b r e0]] | 
[{oct.} : [o0 k t u1 b r e0]] | 
[{nov.} : [n o0 v j e1 m b r e0]] | 
[{dic.} : [d i0 c j e1 m b r e0]] | 
# Comon
[{dr.} : [d o0 k t o1 r]] | 
[{dra.} : [d o0 k t o1 r a]] | 
[{lcdo.} : [l i0 c e0 n c j a1 dh o0]] | 
[{lcda.} : [l i0 c e0 n c j a1 dh a0]] | 
[{sr.} : [s e0 ni o1 r]] | 
[{sres.} : [s e0 ni o1 r e0 s]] | 
[{sra.} : [s e0 ni o1 r a0]] | 
[{sras.} : [s e0 ni o1 r a0 s]] | 
[{srta.} : [s e0 ni o0 r i1 t a0]] | 
[{ud.} : [u0 s t e1 d]] | 
[{uds.} : [u0 s t e1 dh e0 s]] | 
# Adverbs with one syllable after suffix:
[{cruelmente} : [k r w e1 l m e1 n t e0]] | 
[{fielmente} : [f j e1 l m e1 n t e0]] | 
[{grismente} : [g r i1 s m e1 n t e0]] | # Poem: Los ausentes soplan grismente y la noche es densa
[{vilmente} : [v i1 l m e1 n t e0]] | 
[{etc.} : [e0 t c e1 t e0 r a0]] ; 

define Stage1 
TranscribeVowel @-> ; 

define Stage2 
TranscribeConsonant @-> ; 

define ModifierRule 
modifier @-> 0 || .#. ([Consonant|i|u]) _ vocal , Consonant _ (Consonant) .#. , vocal _ (Consonant) .#. ; 

define RenameConsonants 
YAsVowel @-> i ,, 
zh @-> c ; 

define Stage3 
SpanishRules @-> ; 

define StressFormRule 
stresses @-> || .#. _ (modifier) Stressf .#. ; 

define ohiRule 
# prohibamos, prohibido, prohibiciones, prohibición, prohibid, prohibida, etc.
i0:j @-> || [o0 modifier] _ ; 

# adverbs
source include/textproc/adverbs_exceptions.foma
define mente [m [e1|e0] n t e0] ; 
define AdverbRule 
[..] @-> adv || 
.#. [[pc+ vocal2+ ([pc|vocal2]+)]|[vocal2+ pc+ (vocal2+)]]^2* | [vocal2+ pc+ vocal2] | [pc+ vocal2 vocal2] _ mente .#. ; 

define NotAdverb 
adv @-> 0 || .#. _ PhWord .#. ; 

define StressAdverb 
[m e0 n t e0]:[m e1 n t e0] @-> || .#. PhWord "adv" _ .#. , .#. PhWord _ exc .#. ; 

define rules 
deshChange .o. 
UnstressedWordsTag .o. 
AdverbExceptionTag .o. 
StressedForms .o. 
Stage1 .o. 
Stage2 .o. 
ModifierRule .o. 
[Y @-> y] .o. 
[%' @-> 0] .o. 
Stage3 .o. 
WordStartRules .o. 
WordMidRules .o. 
WordEndRules .o. 
RenameConsonants .o. 
approximants .o. 
unstress .o. 
dhipthongStress .o. 
MonosylStress .o. 
StressFormRule .o. 
SpanishStress .o. 
SpanishStress2 .o. 
ohiRule .o. 
[modifier|Stressf @-> 0] .o. 
AdverbRule .o. 
NotAdverb .o. 
SpanishStressPost .o. 
SpanishStressPost2 .o. 
StressAdverb .o. 
[[adv|exc] @-> 0] ; 

regex 
[exceptions .P. rules] ; 