# Copyright (C) 2024-2025  Mateo Cedillo <angelitomateocedillo@gmail.com>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 2.1 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

source variables.foma

define Apos %'|%‘|%’ ; 

define LetterSequence [GraphemeConsonant - [y|Y]]+ /Apos ; 

define ExceptionalWord 
[[a|A] %. [m|M] %.] | 
[[p|P] %. [m|M] %.] | 
[[a|A] %. [c|C] %.] | 
[[d|D] %. [c|C] %.] | 
[L u n %.] | [M a r %.] | [M i é %.] | [J u e %.] | [V i e %.] | [S á b %.] | [D o m %.] | 
[e n e %.] | [f e b %.] | [a b r %.] | [m a y %.] | [j u n %.] | [j u l %.] | [a g o %.] | [s e p %.] | [o c t %.] | [n o v %.] | [d i c %.] | 
[[e|E] [t|T] [c|C] %.] | 
[[w|W] [i|I] %- [f|F] [i|I]] ; 
# Exceptions for apple product names. Avoid word splitting (see below)
define ExceptionalWord2 [[P h o n e]|[P [a|o] d]] ; 
#define year Digit^<4 ; 
#define DayOrMonth Digit^<2 ; 
#define dateSep %/ ; 
define DecimalSeparator %.|%,;
define Word [Letter+ [Apos Letter+]*] | ExceptionalWord ;
define Symbol \[Letter | Digit] ; 
#define Date [Digit^2 dateSep Digit^2 dateSep Digit^4] ; 

define Stage1 
Word @-> ... word || _ .#. | \Letter ,, 
#FormattedNumber @-> ... num || _ .#. | \Digit ,, 
#UnformattedNumber @-> ... num ,, 
Number @-> ... num ,, 
%0 Digit+ @-> ... dig ,, 
%0 @-> ... num ,, 
# We will apply this future new token for Spanish, called date, when someone helps me with cpp code for it.
# We did an experimental approach by adding date support in number processing. However, I have concluded that the best option is that date support must have its own token type, because. a) we can create a switcher in the configuration file easily to enable or disable if the user wants date reading, and b) it is more easy to control because it is a separated way, outside numbers.
#Date @-> ... date || _ ,, 
Symbol @-> ... sym || _ ; 

define Stage2 
# SplitWordsWhenCamelCase, so voices canInterpretAsSeparateWords InsteadOfOnlyOne
[..] -> word || 
[SmallLetter|SmallVowel] _ [CapitalLetter|CapitalVowel] , 
[CapitalLetter|CapitalVowel] _ [CapitalLetter|CapitalVowel] [SmallLetter|SmallVowel] ,, 
word -> num || [.#.|sym] ESRomanNumeral _ [.#.|[? sym]] ; 

define Stage3 
# Custom LSeq handling:
word -> lseq || 
.#. ([Symbol sym]|[Word word]) CapitalLetter^2 CapitalVowel _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter^3 CapitalVowel _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter^4 CapitalVowel _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter^5 CapitalVowel _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalVowel CapitalLetter^2 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalVowel CapitalLetter^3 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalVowel CapitalLetter^4 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalVowel CapitalLetter^5 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter CapitalVowel CapitalLetter^2 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter CapitalVowel CapitalLetter^3 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter CapitalVowel CapitalLetter^4 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter^2 CapitalVowel CapitalLetter^2 _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) X CapitalVowel CapitalLetter _ (Symbol sym) (Symbol sym) (Symbol sym) .#. , 
.#. ([Symbol sym]|[Word word]) CapitalLetter CapitalVowel W _ (Symbol sym) (Symbol sym) (Symbol sym) .#. ; 

define Stage4 
word -> lseq || [.#.|word|num|dig|sym] LetterSequence _ ,, 
sym -> num || [sym|.#.] %- _ Digit,
sym -> num || [sym|.#.] %+ _ Digit,
num DecimalSeparator _ Digit+,,
sym -> 0 || [%: sym %/] _ [%/ sym Word] ,, 
num -> dig || Digit^28 _ ,, 
[num:0] OrdChar [sym:num] -> ; 

define Stage5 
word -> lseq || .#. _ , .#. GraphemeVowel _ Number ,, 
sym -> num || 
[word|lseq] [%.|%/] _ [Word|LetterSequence] , 
%: sym [%/^2] _ Word ,, 
word -> 0 || 
# Apple product names.
{i} _ ExceptionalWord2 word , 
# Miscellaneous: YouTube and others.
[[y|Y] o u] _ [[t|T] u b e] ; 

source special_chars.foma

regex 
NormalizeCharacters.o. 
Stage1 .o. 
Stage2 .o. 
Stage3 .o. 
Stage4 .o. 
Stage5 ; 